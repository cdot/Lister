buildscript {
    ext.kotlin_version = '1.4.30-M1'
    repositories {
        google()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    signingConfigs {
        release {
            // Set the following in your local.properties
            storeFile file(localProperties['KEY_STORE_FILE'])
            storePassword localProperties['KEY_STORE_PASSWORD']
            keyAlias localProperties['KEY_ALIAS']
            keyPassword localProperties['KEY_PASSWORD']
        }
    }
    compileSdkVersion 30
    buildFeatures.viewBinding = true

    defaultConfig {
        applicationId 'com.cdot.lists'
        minSdkVersion 23
        targetSdkVersion 30
        // Increment the versionCode on each release - it is not shown to the user
        versionCode 5
        versionName "v1.5 Eas"
        buildConfigField "long", "BUILD_TIME",  System.currentTimeMillis() + "L"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            signingConfig signingConfigs.debug
            versionNameSuffix '.debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            systemProperty 'robolectric.logging.enabled', true
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.fragment:fragment:1.2.5"
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-drive:17.0.0'
    implementation 'com.opencsv:opencsv:5.0'
    implementation 'com.github.johnkil.print:print:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Emulator tests
    // androidTestImplementation 'junit:junit:4.13'
    // androidTestImplementation 'androidx.test:core:1.3.0'

    // Roboelectric and JVM tests
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation "org.json:json:20190722"
    testImplementation "org.hamcrest:hamcrest-junit:2.0.0.0"
    testImplementation "uk.co.datumedge:hamcrest-json:0.1"
    testImplementation "org.skyscreamer:jsonassert:1.5.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}
